import "imdb.malloy"


source: movies_queries is movies + {

  // Queries...  

  // Top Movies by numVotes (total number of people that liked it)
  query: top20 is by_title + { limit: 20 } 

  query: top20_horror is top20 + { 
    where: genres.value = 'Horror'
  }

  // Movie Stars
  query: top_stars is by_name + { 
    limit: 20 
  } 

  query: top_stars_comedy is top_stars + {
    where: genres.value = 'Comedy'
  }

  // Movie Stars Titles time and jobs
  query: top_stars_jobs_and_more_dashboard is by_name + { 
    where: ratings.numVotes > 50000  // bug: https://github.com/duckdb/duckdb/issues/3734
    nest: 
      by_title + { limit: 5}
      by_year_line_chart is by_year
      by_job_category
    limit: 20 
  } 

  query: top_comedy_star_dashboard is top_stars_jobs_and_more_dashboard + {
    where: genres.value = 'Comedy'
  }

  // top Directors and their 5 best movies
  query: top_directors is by_name + {
    where: principals.category = 'director'
    nest: by_title + { limit: 5}
  }

  // top Comedy Directors and their 5 best movies
  query: top_comedy_directors is  by_name + {
    where: principals.category = 'director', genres.value = 'Comedy'
    nest: by_title + { limit: 5}
  }

  // by Genre, top movies, directors and actors
  query: genres_top_movies_directors_and_actors is  by_genre + {
    where: ratings.numVotes > 30000 // bug: https://github.com/duckdb/duckdb/issues/3734
    nest: by_title + { limit: 5}
    nest: by_director is by_name + {
      where: principals.category = 'director'
      limit: 5
    }
    nest: by_actor is by_name + {
      where: principals.category ~ 'act%'
      limit: 5
    }
  }

  query: g2 is {
    where: ratings.numVotes > 30000 // bug: https://github.com/duckdb/duckdb/issues/3734
    // group_by: genres.value
    aggregate: title_count, total_ratings
    // nest: by_title + { limit: 5}
    nest: by_director is {
      where: principals.category = 'director'
      group_by: 
        principals.people.primaryName, 
        principals.nconst
      aggregate:
        total_ratings
        title_count
      limit: 5
    }
  }
}
