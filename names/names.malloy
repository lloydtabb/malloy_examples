source: names_table is table('duckdb:data/usa_names.parquet') {
  measure: population is `number`.sum()
  dimension: decade is floor(`year`/10)*10

  query: by_name is {
    group_by: name
    aggregate: population
    limit: 10
  }

  query: by_state is {
    group_by: state
    aggregate: population
  }

  query: by_gender is {
    group_by: gender
    aggregate: population
  }

  query: by_year is {
    group_by: `year`
    aggregate: population
    order_by: 1 asc
  }

  query: by_decade is {
    group_by: decade
    aggregate: population
    order_by: 1 asc
  }

  query: name_as_pct_of_pop is {
    aggregate: population
    nest: by_name
  }
  -> {
    project: 
      by_name.*,
      percentage_of_population is by_name.population / population * 100 
    order_by: 3 desc
    limit: 10
  }

  query: male_names is by_name {? gender = 'M'}
  query: female_names is by_name {? gender = 'F'}

  query: top_names_by_state_ea_gender is by_state {
    nest: male_names
    nest: female_names
    limit: 10
  }

  query: name_dashboard is by_name {
    nest: 
      by_decade
      by_state
      by_gender
    limit: 10
  }
}

query: j_names is names_table -> name_dashboard {? name ~ r'J'}



